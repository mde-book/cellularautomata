using kermeta::standard
using kermeta::emfpersistence
using ca_vm
using ca_rule
using ca_initialization
using ca::evaluator
using kermeta::io::StdIO => stdio

package cellular_automata::vm{
	class GridGenerator
	{
		@main ""
		operation generateGrid() : Void is do 
			var g : Grid := generateMooreSquareBoundedGrid(3)
			saveGrid(g)
		end
		
		@main "platform:/resource/org.kermeta.language.sample.cellularautomata/src/main/samples/2D_3x3_Moore_0.CA_Initialization.xmi"
		operation generateInitialGridForAutomataFile(automataFile : String) : Void is do 
			var resourceSet : ResourceSet init ResourceSet.new
	        var resource : Resource init 
	        	resourceSet.createResource(automataFile, "platform:/resource/org.kermeta.language.sample.cellularautomata/src/main/ecore/CA_Initialization.ecore")
			resource.load(void)
			var ca : CellularAutomataInitialization := resource.getContents.one.asType(CellularAutomataInitialization)
			saveGrid(generateInitialGridForAutomata(ca))
			
			
		end
		
		operation generateInitialGridForAutomata(automata : CellularAutomataInitialization) : Grid is do
			result := void
			var regularGeometry : RegularGeometry
			regularGeometry ?= automata.geometry
			if not regularGeometry.isVoid then				
				if ((regularGeometry.neighborsNumber == 4) and (not regularGeometry.isCircular) and (regularGeometry.dimension == 2)) then
					result := generateVonNeumannSquareBoundedGrid(regularGeometry.worldSize)
				else if ((regularGeometry.neighborsNumber == 8) and (not regularGeometry.isCircular) and (regularGeometry.dimension == 2)) then
					result := generateMooreSquareBoundedGrid(regularGeometry.worldSize)
				else 
					stdio.errorln(" Generic configuration not supported yet. Currently supported configurations : dimension=2; neighborsNumber=4|8; not circular")
				end end
				if not result.isVoid then
					// fill default values
					result.cells.each{cell |
						if not automata.seedRule.isVoid then
							var context : Context := Context.new.initialize(result, cell)
							cell.~value := automata.seedRule.~value.evaluate(context)
						end
					}
					if regularGeometry.dimension <= 2 then
						// visualize result using ascii art
						var asciiArtVisualizer : cellular_automata::visualizers::SimpleAsciiArt2DVisualizer := cellular_automata::visualizers::SimpleAsciiArt2DVisualizer.new.initialize(false)
						asciiArtVisualizer.visualizeRegular2DGrid(regularGeometry.worldSize, result)
					end
				end
			else
				stdio.errorln(" Generic configuration not supported yet. Only RegularGeometry can be generated automatically by this.")
			end				
		end
		
		operation generateVonNeumannSquareBoundedGrid(gridSize : Integer) : ca_vm::Grid is do 
			stdio.writeln("Generating a square Grid...")
			var g : Grid := Grid.new
			
			var cellNumber : Integer := gridSize * gridSize
			stdio.writeln("Generating "+cellNumber.toString+" Cells...")
			cellNumber.times{ i | 
				var cell : Cell := Cell.new
				cell.globalPosition := i
				g.cells.add(cell)
			}
			stdio.writeln("Generating bounded VonNeumann neighborhood for "+cellNumber.toString+" Cells...")
			cellNumber.times{ i | 
				var currentCell : Cell := g.cells.at(i)
				var currentLine : Integer :=  i.div(gridSize)
				var currentColumn : Integer := i.mod(gridSize)
				//stdio.writeln("  "+ i.toString+ " line="+currentLine.toString+ ", col="+currentColumn.toString)
				var maxCol : Integer := gridSize-1
				if(currentLine > 0) then // add north 
					currentCell.neighbors.add(g.cells.at(i-gridSize))
				end
				if(currentColumn < maxCol) then // add east 
					currentCell.neighbors.add(g.cells.at(i+1))
				end
				if(currentLine < maxCol) then // add south 
					currentCell.neighbors.add(g.cells.at(i+gridSize))
				end
				if(currentColumn > 0) then // add west 
					currentCell.neighbors.add(g.cells.at(i-1))
				end
				currentCell.~value := 0
			}
			result := g
		end
		operation generateMooreSquareBoundedGrid(gridSize : Integer) : ca_vm::Grid is do 
			stdio.writeln("Generating a square Grid...")
			var g : Grid := Grid.new
			
			var cellNumber : Integer := gridSize * gridSize
			stdio.writeln("Generating "+cellNumber.toString+" Cells...")
			cellNumber.times{ i | 
				var cell : Cell := Cell.new
				cell.globalPosition := i
				g.cells.add(cell)
			}
			stdio.writeln("Generating bounded Moore neighborhood for "+cellNumber.toString+" Cells...")
			cellNumber.times{ i | 
				var currentCell : Cell := g.cells.at(i)
				var currentLine : Integer :=  i.div(gridSize)
				var currentColumn : Integer := i.mod(gridSize)
				//stdio.writeln("  "+ i.toString+ " line="+currentLine.toString+ ", col="+currentColumn.toString)
				var maxCol : Integer := gridSize-1
				if(currentLine > 0) then // add north 
					currentCell.neighbors.add(g.cells.at(i-gridSize))
				end
				if(currentLine > 0 and currentColumn < maxCol) then // add north/east 
					currentCell.neighbors.add(g.cells.at(i-gridSize+1))
				end
				if(currentColumn < maxCol) then // add east 
					currentCell.neighbors.add(g.cells.at(i+1))
				end
				if(currentColumn < maxCol and currentLine < maxCol) then // add south/east 
					currentCell.neighbors.add(g.cells.at(i+1+gridSize))
				end
				if(currentLine < maxCol) then // add south 
					currentCell.neighbors.add(g.cells.at(i+gridSize))
				end
				if(currentLine < maxCol and currentColumn > 0) then // add south/west
					currentCell.neighbors.add(g.cells.at(i+gridSize-1))
				end
				if(currentColumn > 0) then // add west 
					currentCell.neighbors.add(g.cells.at(i-1))
				end
				if(currentColumn > 0 and currentLine > 0) then // add north/west 
					currentCell.neighbors.add(g.cells.at(i-1-gridSize))
				end
				currentCell.~value := 0
			}
			result := g
		end
		 
		
		operation saveGrid( grid : ca_vm::Grid) : Void is do
			var resourceSet : ResourceSet init ResourceSet.new
	        var resource : Resource init 
	        	resourceSet.createResource("platform:/resource/org.kermeta.language.sample.cellularautomata/target/results/grid.xmi", 
	        							 					 "platform:/resource/org.kermeta.language.sample.cellularautomata/src/main/ecore/CA_VM.ecore")       							 					
		 	// Define a Root ...
		 	resource.getContents.add(grid)
		 		 	
		 	resource.save(void)
		end
	}
}