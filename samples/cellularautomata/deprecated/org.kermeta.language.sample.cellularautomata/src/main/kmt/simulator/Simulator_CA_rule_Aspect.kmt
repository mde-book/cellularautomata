using kermeta::standard
using ca::evaluator
using kermeta::io::StdIO => stdio

package ca_rule{
	aspect class CellularAutomata
	{
		operation applyRules(context : Context) : Void is do 
			context.currentGrid.cells.each{ cell |
				context.currentCell := cell
				// select the rule that applies (there must be maximum one)
				var rule : Rule := rules.select{r |
					r.isApplicableForValue(cell.~value)
				}.one
				if rule.isVoid then
					// no rule, report the current value as the future
					cell.futureValue := cell.~value
				else
					cell.futureValue := rule.evaluate(context)
				end
			}
			// apply the change
			context.currentGrid.cells.each{ cell |
				cell.~value := cell.futureValue
			}
		end
	}
	
	
	
	aspect class Rule {
		operation isApplicableForValue(val : Integer) : Boolean is do			
			if filter.isVoid then
				result := true
			else 
				result := filter.matchesValue(val)
			end
		end
		
	}
}