-- @path CURRENT=/org.kermeta.language.sample.cellularautomata.vm.model/metamodel/vm.ecore
-- @path NEXTSTEP=/org.kermeta.language.sample.cellularautomata.vm.model/metamodel/vm.ecore

module gameoflife_step;
create OUT : NEXTSTEP from IN : CURRENT;
rule copyUniverse {
	from	s : CURRENT!Universe
	to 		t : NEXTSTEP!Universe (
			cells <- s.cells
		)
}
-- MDE_BOOK_START
rule copyDeadCells {
	from	s : CURRENT!Cell (s.val = 0) 
	to 		t : NEXTSTEP!Cell (
			val <- if(s.neighbors->select(n | n.val = 1)->size() = 3) 
				   then 1 else 0 endif,
			neighbors <- s.neighbors
		)
}
rule copyLiveCells {
	from	s : CURRENT!Cell (s.val = 1 ) 
	to 		t : NEXTSTEP!Cell (
			val <- if(   s.neighbors->select(n | n.val = 1)->size() = 2
				      or s.neighbors->select(n | n.val = 1)->size() = 3) 
				   then 1 else 0 endif,
			neighbors <- s.neighbors
		)
}
-- MDE_BOOK_END